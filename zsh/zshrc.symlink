#zmodload zsh/zprof # NOTE: uncomment for profiling

export DOTFILES="$HOME/.dotfiles"
export GOPATH="${HOME}/gocode"
export XDG_DATA_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}"
export PATH="${GOPATH}/bin:${DOTFILES}/bin:${PATH}"
export EDITOR="nvim"
export SKIM_DEFAULT_COMMAND="git ls-tree -r --name-only HEAD || fd --type f ."
export HISTFILE=~/.zsh_history
export HISTSIZE=10000
export SAVEHIST=10000
#export DEBUG_GIT_PROMPT=true
export GIT_PROMPT_SHELL=true

setopt APPEND_HISTORY
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS

# aliases
alias b='bat'
alias g='git'
alias gd='git diff'
alias gs='git status'
alias l='exa'
alias ll='exa -l'
alias la='exa -a'
alias vc='e ~/.config/nvim/init.vim'

alias shed="docker run --name shed -v ${PWD}:/x -w /x -ti --rm aignas/shed"

wexec() {
    exts="$1";shift
    watchexec --clear --exts "$exts" --restart "wrap $*"
}
alias bazelw="wexec go,bazel,bzl bazel"
alias cargow="wexec toml,rs cargo"
# Original idea based on: https://github.com/rust-lang/rfcs/issues/2324,
# however it would be great to have a 'cargo doc' to include the stdlib docs
# instead of linking to the online ones.
cargodocw () {
    # pipe-tar is so fast for lots of small files
    src="$(dirname "$(rustup doc --path)")"
    dest="./target/doc"
    echo "Copying standard rust documentation:"
    echo "$src -> $dest"
    rm -rf "$dest"
    mkdir -p "$dest"
    (cd "$src" && tar -cf - .) | (cd "$dest" && tar -xpf -)

    cargow doc --all --document-private-items $@
}

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit -d
zstyle 'history' 'completion' 'git'
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' insert-tab pending
compdef g=git
compdef l=exa

source "${DOTFILES}/zsh/git_prompt.zsh"

setopt promptsubst
# http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
# A nice dir shortening: https://unix.stackexchange.com/questions/273529
PROMPT='%F{blue}%(5~|%-1~/â€¦/%3~|%4~)%f %{$prompt_git_status%}
%(?.%F{magenta}.%F{red})%(!.#.$)%f '

trysource() { [ -f "${1}" ] && source "${1}"; }
trysource "${HOME}/.localrc"
trysource "${HOME}/.skim/shell/completion.zsh"
trysource "${HOME}/.skim/shell/key-bindings.zsh"
trysource /usr/share/skim/completion.zsh
trysource /usr/share/skim/key-bindings.zsh

# Functions, OS specific, so that they do not appear in the path.
[[ "$(get_os)" == "ArchLinux" ]] && aur() {
case "$1" in
    "--help"|"-h")
        cat <<EOF
AUR downloading function.

Usage:
  aur [-s|--search] [PACKAGE_NAME]
EOF
return
;;
''|'-s'|'--search')
    local input
    if [[ -z $2 ]]; then
        echo -n "enter AUR search term: "
        read input
    else
        input="$2"
    fi
    selection=$(
    curl --silent "https://aur.archlinux.org/rpc/?v=5&type=search&arg=$input" |
        jq -r '.results[] | "\(.NumVotes)\t\(.Name)\t\(.Description)"' |
        awk '{pop=$1;name=$2; $1="";$2=""; printf "%5d \x1b[34;1m%s\x1b[m\t%s\n", pop, name, $0}' |
        sort -n -r |
        column -t -s $'\t' |
        sk --ansi |
        awk '{print $2}')
            aur "$selection"
            return
            ;;
        *)
            local dir="${HOME}/aur/$1"
            [[ ! -d "$dir" ]] && git clone "https://aur.archlinux.org/$1.git" "$dir"

            pushd "$dir"
            git checkout .
            git pull --rebase origin master
            ;;
    esac
}
