# My zshrc configuration. Use or reuse however you like.
#       by gns-ank

function profile() {
    echo "PROFILE: $@"
}

# load zgen
source "${HOME}/src/github/zgen/zgen.zsh"

# shortcut to this dotfiles path is $ZSH
export ZSH_DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/src

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH_DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Normal completion behaviour
zstyle ':completion:*' completer _complete
# Case insensitive matching and matching can occur on both sides of the current
# word.
zstyle ':completion:*' matcher-list '' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' '+l:|=* r:|=*'
# See: http://stackoverflow.com/a/22627273/1105870

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# check if there's no init script
if ! zgen saved; then
    echo "Creating a zgen save"

    # Oh-my-zsh plugins
    zgen oh-my-zsh plugins/sudo
    zgen oh-my-zsh plugins/github
    zgen oh-my-zsh plugins/command-not-found
    zgen oh-my-zsh plugins/python
 
    # completions
    zgen load zsh-users/zsh-completions src
    zgen load zsh-users/zsh-syntax-highlighting
    zgen load zsh-users/zsh-history-substring-search

    # theme
    # Note: we include the async.zsh script in order to run git commands
    # asynchronously.
    zgen load sindresorhus/pure async.zsh
    zgen load sindresorhus/pure

    # save all to init script
    zgen save
fi

unset config_files

zstyle \
    'history' \
    'completion' \
    'git' \
    'history-substring-search'

# Load the fuzzy finder
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh


# vim: tw=80:ft=zsh
