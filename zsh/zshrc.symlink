#
# My zshrc configuration. Use or reuse however you like.
#       by gns-ank

# shortcut to this dotfiles path is $ZSH
export DOTFILES="$HOME/.dotfiles"
export DOTFILES_OS="$(${DOTFILES}/bin/get_os)"
export SH_PLUGIN="${DOTFILES}/sh-plugins/bin"
export GOPATH="${HOME}/gocode"
export PATH="${GOPATH}/bin:${DOTFILES}/bin:${PATH}"
[[ -d ${HOME}/.skim/bin ]] && export PATH="${HOME}/.skim/bin:${PATH}"

export EDITOR="nvim"
export SKIM_DEFAULT_COMMAND="fd ."

# {{{ config
export HISTFILE=~/.zsh_history
export HISTSIZE=10000
export SAVEHIST=10000

setopt APPEND_HISTORY           # adds history
# adds history incrementally and share it across sessions
setopt INC_APPEND_HISTORY SHARE_HISTORY
setopt EXTENDED_HISTORY         # add timestamps to history

# don't record dupes in history
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS
# }}}
# {{{ aliases
alias b='bat --theme=zenburn'
alias g='git'
alias h='hub'
alias gf='git fetch --all'
alias k='kubectl'
alias se='sudo -E e'
alias vc='e ~/.config/nvim/init.vim'
alias x='exa'
alias xx='exa -l'
# }}}

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' insert-tab pending
compdef g=git

zstyle \
  'history' \
  'completion' \
  'git'

trysource() {
  [ -f "${HOME}/${1}" ] && source "${HOME}/${1}"
}

plug() {
  "${SH_PLUGIN}/$1"
}

# The following are stolen form @agkozak's wonderful prompt:
# https://github.com/agkozak/agkozak-zsh-prompt, which got this functionality
# was specifically added by @psprint in
# https://github.com/agkozak/agkozak-zsh-prompt/pull/11
#
# This contains just the bare minimum in order to get everything working on my
# machine.

precmd() {
  psvar=()
  setopt LOCAL_OPTIONS NO_IGNORE_BRACES
  typeset -g VCS_INFO_FD=13371
  exec {VCS_INFO_FD}< <(plug git_prompt)
  zle -F "$VCS_INFO_FD" _vcs_info_callback
}
function _vcs_info_callback() {
  setopt LOCAL_OPTIONS NO_IGNORE_BRACES

  local FD="$1" response
  IFS='' builtin read -rs -d $'\0' -u "$FD" response

  # Withdraw callback and close the file descriptor
  zle -F ${FD}; exec {FD}<&-

  # Make the changes visible
  psvar[1]="$response"
  zle && zle reset-prompt
}

setopt promptsubst
# http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
PROMPT='%F{blue}%~%f %{$psvar[1]%}
%F{magenta}â¯%f '

if [[ "${DOTFILES_OS}" == "ArchLinux" ]]; then
  aurd() {
    p="${HOME}/aur/$1"
    [[ ! -d "$p" ]] && git clone "https://aur.archlinux.org/$1.git" "$p"
    pushd "$p" && git pull --rebase origin master
  }
fi

# trysource ${HOME}/.fzf.zsh
trysource .localrc
trysource .skim/shell/completion.zsh
trysource .skim/shell/key-bindings.zsh

#{{{ Functions

# gco - checkout git branch/tag
gco() {
  local targets prefix
  case $1 in
    t|tag)
      prefix=tag
      targets=$(git tag || return)
      ;;
    ''|b|branch)
      prefix=branch
      targets=$(git branch --all |
        grep -v HEAD |
        sed "s/.* //" |
        sed "s#remotes/[^/]*/##" || return)
      ;;
    h|help)
      cat <<EOF
Interactive checkout a branch or a tag.

Subcommands:
 [|b|branch]: branch checkout
 [t|tag]: tag checkout
EOF
      return
      ;;
    *)
      git checkout $@
      return
  esac
  [[ -z ${targets} ]] && return
  target=$(echo "$targets" |
    sort -u |
    awk -v prefix="$prefix" '{print "\x1b[31;1m" prefix "\x1b[m\t" $1}' |
    sk --no-hscroll --ansi -d "\t" -n 2) || return
  git checkout "$(echo "$target" | awk '{print $2}')"
}

rsw () {
  echo "Running local CI [ hit CTRL+C to stop ]"
  {
    fd Cargo
    fd -e rs
  } | entr -d cargo "$@"
}

rsd () {
  # Original idea based on: https://github.com/rust-lang/rfcs/issues/2324,
  # however it would be great to have a 'cargo doc' to include the stdlib docs
  # instead of linking to the online ones.
  readonly local docpath="$(dirname "$(rustup doc --path)")/"
  echo "Syncing $docpath to ./target/doc..."
  rsync -r "$docpath" ./target/doc/
  echo "Done"
  rsw doc --all --document-private-items $@
}
#}}}

# vim: tw=80:ft=zsh:sw=2:ts=2:fdm=marker
