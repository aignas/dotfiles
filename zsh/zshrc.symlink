# My zshrc configuration. Use or reuse however you like.
#       by gns-ank

function profile() {
    echo "PROFILE: $@"
}

# shortcut to this dotfiles path is $ZSH
export ZSH_DOTFILES=$HOME/.dotfiles
source ${ZSH_DOTFILES}/bin/dot

# your project folder that we can `c [tab]` to
export PROJECTS=~/src

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH_DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Normal completion behaviour
zstyle ':completion:*' completer _complete
# Case insensitive matching and matching can occur on both sides of the current
# word.
zstyle ':completion:*' matcher-list '' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' '+l:|=* r:|=*'
# See: http://stackoverflow.com/a/22627273/1105870

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# My plugins
source ~/.zplug/init.zsh

zplug "plugins/sudo", from:oh-my-zsh
zplug "plugins/github", from:oh-my-zsh
zplug "plugins/command-not-found", from:oh-my-zsh
zplug "plugins/python", from:oh-my-zsh

# completions
zplug "zsh-users/zsh-completions"
zplug "zsh-users/zsh-syntax-highlighting"
zplug "zsh-users/zsh-history-substring-search"

# theme
# Note: we include the async.zsh script in order to run git commands
# asynchronously.
zplug "sindresorhus/pure", use:async.zsh
zplug "sindresorhus/pure"

# Install plugins if there are plugins that have not been installed
if ! zplug check --verbose; then
    printf "Install? [y/N]: "
    if read -q; then
        echo; zplug install
    fi
fi

# Then, source plugins and add commands to $PATH
zplug load --verbose

unset config_files

zstyle \
    'history' \
    'completion' \
    'git' \
    'history-substring-search'

# Load the fuzzy finder
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# .NET Core related stuff
[ -s "${HOME}/.dnx/dnvm/dnvm.sh" ] && . "${HOME}/.dnx/dnvm/dnvm.sh" # Load dnvm

# vim: tw=80:ft=zsh
