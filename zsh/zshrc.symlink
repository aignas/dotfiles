#
# My zshrc configuration. Use or reuse however you like.
#       by gns-ank

# shortcut to this dotfiles path is $ZSH
export DOTFILES="$HOME/.dotfiles"
export DOTFILES_OS="$(${DOTFILES}/bin/get_os)"
export SH_PLUGIN="${DOTFILES}/sh-plugins/bin"
export GOPATH="${HOME}/gocode"
export PATH="${GOPATH}/bin:${DOTFILES}/bin:${PATH}"
[[ -d ${HOME}/.skim/bin ]] && export PATH="${HOME}/.skim/bin:${PATH}"

export EDITOR="nvim"
export SKIM_DEFAULT_COMMAND="fd ."

# {{{ config
export HISTFILE=~/.zsh_history
export HISTSIZE=10000
export SAVEHIST=10000

setopt APPEND_HISTORY           # adds history
# adds history incrementally and share it across sessions
setopt INC_APPEND_HISTORY SHARE_HISTORY
setopt EXTENDED_HISTORY         # add timestamps to history

# don't record dupes in history
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS
# }}}
# {{{ aliases
alias b='bat --theme=zenburn'
alias g='git'
alias h='hub'
alias gf='git fetch --all'
alias k='kubectl'
alias se='sudo -E e'
alias vc='e ~/.config/nvim/init.vim'
alias x='exa'
alias xx='exa -l'
# }}}

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' insert-tab pending
compdef g=git

zstyle \
  'history' \
  'completion' \
  'git'

trysource() {
  [ -f "${HOME}/${1}" ] && source "${HOME}/${1}"
}

plug() {
  name=$1; shift
  "${SH_PLUGIN}/$name" $@
}

# The following are stolen form @agkozak's wonderful prompt:
# https://github.com/agkozak/agkozak-zsh-prompt, which got this functionality
# was specifically added by @psprint in
# https://github.com/agkozak/agkozak-zsh-prompt/pull/11
#
# This contains just the bare minimum in order to get everything working on my
# machine.

precmd() {
  psvar=()
  setopt LOCAL_OPTIONS NO_IGNORE_BRACES

  typeset -g VCS_INFO_FD=${RANDOM}
  exec {VCS_INFO_FD}< <(plug git_prompt --no-status)
  zle -F "$VCS_INFO_FD" _vcs_info_callback_branch

  typeset -g VCS_INFO_FD=${RANDOM}
  exec {VCS_INFO_FD}< <(plug git_prompt --no-branch --no-diff)
  zle -F "$VCS_INFO_FD" _vcs_info_callback_status
}
function _vcs_info_callback_branch() { _vcs_info_callback $1 1 }
function _vcs_info_callback_status() { _vcs_info_callback $1 2 }
function _vcs_info_callback() {
  setopt LOCAL_OPTIONS NO_IGNORE_BRACES

  local FD="$1" response
  IFS='' builtin read -rs -d $'\0' -u "$FD" response

  zle -F ${FD}; exec {FD}<&- # Withdraw callback and close the file descriptor

  # Make the changes visible and redraw only if there is something to draw
  if [[ -z $response ]]; then
    return
  fi
  psvar[$2]="$response"
  zle && zle reset-prompt
}

setopt promptsubst
# http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
PROMPT='%F{blue}%~%f %{$psvar[1]%}%{$psvar[2]%}
%F{magenta}â¯%f '

if [[ "${DOTFILES_OS}" == "ArchLinux" ]]; then
  aurd() {
    p="${HOME}/aur/$1"
    [[ ! -d "$p" ]] && git clone "https://aur.archlinux.org/$1.git" "$p"
    pushd "$p" && git pull --rebase origin master
  }
fi

# trysource ${HOME}/.fzf.zsh
trysource .localrc
trysource .skim/shell/completion.zsh
trysource .skim/shell/key-bindings.zsh

#{{{ Functions

rsw () {
  echo "Running local CI [ hit CTRL+C to stop ]"
  {
    fd Cargo
    fd -e rs
  } | entr -d cargo "$@"
}

rsd () {
  # Original idea based on: https://github.com/rust-lang/rfcs/issues/2324,
  # however it would be great to have a 'cargo doc' to include the stdlib docs
  # instead of linking to the online ones.
  readonly local docpath="$(dirname "$(rustup doc --path)")/"
  echo "Syncing $docpath to ./target/doc..."
  rsync -r "$docpath" ./target/doc/
  echo "Done"
  rsw doc --all --document-private-items $@
}
#}}}

# vim: tw=80:ft=zsh:sw=2:ts=2:fdm=marker
