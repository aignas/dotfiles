#zmodload zsh/zprof # NOTE: uncomment for profiling

set -o vi

path_prepend() {
    local -r item="$1"

    [[ ! -d "$item" ]]                    && return # does not exist
    [[ ${path[(ie)$item]} -le ${#path} ]] && return # already in PATH

    path=("$item" $path)
}
trysource() {
    if [[ -f "${1}" ]]; then
        source "${1}"
    fi
}
exists() {
    if [[ -n "${commands[$1]}" ]]; then
        return 0
    fi
    return 1
}

export DOTFILES="$HOME/.dotfiles"
export XDG_DATA_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}"
export NVM_DIR="${HOME}/.nvm"

path_prepend "${HOME}/.cargo/bin"
path_prepend "${HOME}/.local/bin"
export PATH

export EDITOR="vim"
exists nvim && export EDITOR="nvim"
export HISTFILE=~/.zsh_history
export HISTSIZE=10000
export SAVEHIST=10000

setopt APPEND_HISTORY
setopt INC_APPEND_HISTORY
setopt DVORAK
setopt CORRECT
setopt SHARE_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS

# aliases
alias g='git'
alias gd='git diff'
alias gp='git push'
alias gpf='git push -fu'
alias gs='git status --short --branch --show-stash'
alias fixup='git commit --fixup'
alias vc='e ~/.config/nvim/init.lua'
alias se='sudo -E $(which e)'
exists kubectl && alias k='kubectl'
exists terragrunt && alias tg='terragrunt'
exists fd && alias fd='fd --color=never'

exists eza && alias ls='eza --color=never'
alias ll='ls -l'
alias la='ls -a'

exists task && alias t=task

setopt promptsubst
# http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
PROMPT='%n@${${(%):-%m}#1} %~ %B$(git rev-parse --abbrev-ref HEAD 2>/dev/null|| :)%b
%B%(!.#.$)%b '

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit -d
zstyle 'history' 'completion' 'git'
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' insert-tab pending
compdef g=git

trysource "${HOME}/.local.sh"
trysource "${HOME}/.localrc"
if exists fzf-share; then
    source "$(fzf-share)/key-bindings.zsh"
    source "$(fzf-share)/completion.zsh"
fi
exists direnv && eval "$(direnv hook zsh)"
trysource "${NVM_DIR}"/nvm.sh

trysource "$HOME/.nix-profile/etc/profile.d/hm-session-vars.sh"
